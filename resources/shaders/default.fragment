/* Simple fragment shader with a per fragment phong lighting model.
 */ 
#version 130
	
uniform bool useLighting;
struct LightInfo {
	vec4 Position; // Light position in world coords.
	vec3 La; // Ambient light intensity
	vec3 Ld; // Diffuse light intensity
	vec3 Ls; // Specular light intensity
};
uniform LightInfo Light;
	
struct MaterialInfo {
	vec3 Ka; // Ambient reflectivity
	vec3 Kd; // Diffuse reflectivity
	vec3 Ks; // Specular reflectivity
	float Shininess; // Specular shininess factor
};
uniform MaterialInfo Material;

uniform mat4 ModelViewMatrix;
 
// corresponds with output from vertex shader, gets interpolated
in vec3 Normal;
in vec3 VertexEye;
in vec2 TexCoord;

uniform bool useTexture;
uniform sampler2D Tex1;
 
// output to buffer
out vec4 FragColor;

vec4 lightPositionEye;

/*
 * shading with phong model (ambient, diffuse, specular)
 * position and normal in eye coordinates
 */
vec3 phongModel( vec3 position, vec3 norm, out vec3 ambientAndDiff, out vec3 spec )
{
	vec3 s;
	
	// directional light?
	if( Light.Position.w == 0.0 )
		s = normalize(vec3(lightPositionEye));
	else
		s = normalize(vec3(lightPositionEye) - position);
	vec3 v = normalize(-position);
	vec3 r = reflect( -s, norm );
	vec3 ambient = Light.La * Material.Ka;
	float sDotN = max( dot(s,norm), 0.0 );
	vec3 diffuse = Light.Ld * Material.Kd * sDotN;
	spec = vec3(0.0);
	if( sDotN > 0.0 )
		spec = Light.Ls * Material.Ks *
				pow( max( dot(r,v), 0.0 ), Material.Shininess );
	ambientAndDiff = ambient + diffuse;
	return ambientAndDiff + spec;
}

const int levels = 3;
const float scaleFactor = 1.0 / levels;
// toon shading (only ambient and diffuse)
vec3 toonShade( vec3 position, vec3 norm )
{
	vec3 s;
	  
	// directional light?
	if( Light.Position.w == 0.0 )
		s = normalize(vec3(lightPositionEye));
	else
		s = normalize(vec3(lightPositionEye) - position);
		
	vec3 ambient = Light.La * Material.Ka;
	float cosine = max( dot(s,norm) , 0.0 );
	vec3 diffuse = Light.Ld * Material.Kd * floor( cosine * levels ) * scaleFactor;
	
	return ambient + diffuse;
}
 
void main()
{
	vec3 ambientAndDiff, spec;
	vec4 texColor;
	
	// use texture?
	if ( useTexture )
		texColor = texture( Tex1, TexCoord );
		//texColor = texture( Tex1, vec2( 0.5, 0.5 ) );
		//texColor = texelFetch( Tex1, ivec2( 5, 5 ), 1 );
	else
		texColor = vec4(1.0);

	if ( useLighting ) {
		// light position in eye coordinates
		lightPositionEye = ModelViewMatrix * Light.Position;
		phongModel( VertexEye, normalize(Normal), ambientAndDiff, spec );
		//ambientAndDiff = toonShade( VertexEye, normalize(Normal) );
		FragColor = vec4(ambientAndDiff, 1.0) * texColor + vec4(spec, 0.0);
	} else {
		FragColor = vec4(Material.Ka, 1.0) * texColor;
	}
		
	if( FragColor.a < 0.01 )
		discard;
	//FragColor = texColor;
	//FragColor = vec4(texColor.xyz, 1.0);
	//FragColor = vec4(TexCoord.x, TexCoord.y, 0.0, 1.0);
} 
